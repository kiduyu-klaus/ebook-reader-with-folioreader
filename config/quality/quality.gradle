/**
 * Set up Checkstyle and PMD to perform extensive code analysis.
 *
 * Gradle tasks added:
 * - checkstyle
 * - pmd
 *
 * The two tasks above are added as dependencies of the check task so running check will
 * run all of them.
 */

apply plugin: 'checkstyle'
apply plugin: 'pmd'

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle:6.5'
}

def qualityConfigDir = "$project.rootDir/config/quality"
def reportsDir = "$project.buildDir/reports"

check.dependsOn 'checkstyle', 'pmd'

task checkstyle(type: Checkstyle, group: 'Verification', description: 'Runs code style checks') {
    configFile file("$qualityConfigDir/checkstyle/checkstyle-config.xml")
    source 'src'
    include '**/*.java'

    reports {
        xml.required = true
        xml.outputLocation.set(file("$reportsDir/checkstyle/checkstyle.xml"))
    }

    classpath = files()
}

task pmd(type: Pmd, group: 'Verification', description: 'Inspect sourcecode for bugs') {
    ruleSetFiles = files("$qualityConfigDir/pmd/pmd-ruleset.xml")
    ignoreFailures = false
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.required = true
        html.required = true
        xml.outputLocation.set(file("$reportsDir/pmd/pmd.xml"))
        html.outputLocation.set(file("$reportsDir/pmd/pmd.html"))
    }
}
